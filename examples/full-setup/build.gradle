buildscript {
    apply from: '../../version.gradle'
    dependencies {
        classpath group: 'de.dynamicfiles.projects.gradle.plugins', name: 'imagetransform-gradle-plugin', version: "${gradle.imagetransformGradlePluginVersion}", classifier: 'all'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

apply plugin: 'imagetransform-gradle-plugin'

// global registration
transformImages {
    // convert multiple files
    from fileTree('src/main/images').include('*'), {
        toPNG '64x64', 'build/jfx/app/*'
        toICO '64x64', 'build/jfx/app/*'
        toBMP '64x64', 'build/jfx/app/*'
        toICNS '48x48', 'build/jfx/app/*'
    }
    // convert single file to multiple resolutions, resulting filenames will be extendes with resolution
    from 'src/main/images/source.png', {
        toPNG(['64x64', '128x128'], 'build/jfx/app/*')
        toICO(['64x64h', '128x128'], 'build/jfx/app/*') // invalid configuration
        toBMP(['64x64x', '128x128'], 'build/jfx/app/*') // valid configuration ;)
        toICNS(['48x48', '128x128'], 'build/jfx/app/*')
    }
    // convert single file to multiple resolutions, resulting filenames in the same order will be taken
    from file('src/main/images/source.png'), {
        toPNG(['64x64', '128x128'], ['build/jfx/app/target-64.png', 'build/jfx/app/target-128.png'])
        toICO(['64x64', '128x128'], ['build/jfx/app/target-64.ico', 'build/jfx/app/target-128.ico'])
        toBMP(['64x64', '128x128'], ['build/jfx/app/target-64.bmp', 'build/jfx/app/target-128.bmp'])
        toICNS(['48x48', '128x128'], ['build/jfx/app/target-64.icns', 'build/jfx/app/target-128.icns'])
    }
}

// task registration
task convertImages(type: de.dynamicfiles.projects.gradle.plugins.imagetransform.tasks.TransformTask) {
    transformGlobals true // convert global configuration as part of this task
    dryRun false

    // specify task-specific transformations
    addTransformations {
        from 'src/main/images/source.png', {
            // the last "true" appends the resolution as part of the filename
            toPNG('128x128', 'build/jfx/app/*', true)
            toICO('128x128', 'build/jfx/app/*', true)
            toBMP('64x64', 'build/jfx/app/*', true)
            toICNS('48x48', 'build/jfx/app/*', true)
        }
    }
}

